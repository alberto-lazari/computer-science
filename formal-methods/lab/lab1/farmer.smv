MODULE main
	VAR
		farmer:		{left, right};
		goat:		{left, right, eaten};
		wolf:		{left, right};
		cabbage:	{left, right, eaten};
	IVAR
		move: {g, w, c, n};
	ASSIGN
		init(farmer)	:= left;
		init(goat)		:= left;
		init(wolf)		:= left;
		init(cabbage)	:= left;

		next(farmer) := case
			farmer = right	: left;
			farmer = left	: right;
		esac;

		next(wolf) := case
			move = w & wolf = farmer	: next(farmer);
			TRUE						: wolf;
		esac;

		next(goat) := case
			goat = wolf & goat != farmer	: eaten;
			move = g & goat = farmer		: next(farmer);
			TRUE							: goat;
		esac;
		
		next(cabbage) := case
			cabbage = goat & cabbage != farmer	: eaten;
			move = c & cabbage = farmer			: next(farmer);
			TRUE								: cabbage;
		esac;

INVARSPEC !(wolf = right & goat = right & cabbage = right)
